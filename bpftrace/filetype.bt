#!/usr/bin/env bpftrace
/*
 * filetype - Show VFS reads/writes by file type and process.
 *
 * See BPF Performance Tools, Chapter 8, for an explanation of this tool.
 *
 * Copyright (c) 2019 Brendan Gregg.
 * Licensed under the Apache License, Version 2.0 (the "License").
 * This was originally created for the BPF Performance Tools book
 * published by Addison Wesley. ISBN-13: 9780136554820
 * When copying or porting, include this comment.
 *
 * 02-Feb-2019  Brendan Gregg   Created this.
 */

#include <linux/fs.h>

BEGIN
{
	// from uapi/linux/stat.h:
	@type[0xc000] = "socket";
	@type[0xa000] = "link";
	@type[0x8000] = "regular";
	@type[0x6000] = "block";
	@type[0x4000] = "directory";
	@type[0x2000] = "character";
	@type[0x1000] = "fifo";
	@type[0] = "other";
}

kprobe:vfs_read,
kprobe:vfs_readv,
kprobe:vfs_write,
kprobe:vfs_writev
{
	$file = (struct file *)arg0;
	$mode = $file->f_inode->i_mode;
	@[@type[$mode & 0xf000], func, comm] = count();
}

END
{
	clear(@type);
}
